<rule>          ::= Rule name:<rule_name> '::=' '|'? productions:<production>&+{|}
<production>    ::= NamedProduction name:CAP_WORD parts:<part>+
                  | RuleAliasProduction name:<rule_name>
<part>          ::= ModifiedPart actual:<actual> modifier:<modifier>?
                  | ParameterPart name:LOW_WORD ':' part:<part>
<actual>        ::= LiteralPart1 '"' token:STRING '"'
                  | LiteralPart2 "'" token:STRING "'"
                  | SpecialPart token:ALL_CAP_WORD
                  | RuleNamePart token:<rule_name>
<modifier>      ::= QuestionMark '?'
                  | Star '*'
                  | Plus '+'
                  | AmpersandStar '&*' '{' separator:STRING '}'
                  | AmpersandPlus '&+' '{' separator:STRING '}'
<rule_name>     ::= RuleName '<' name:LOW_WORD '>'
